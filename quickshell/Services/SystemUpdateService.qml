pragma Singleton
pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Quickshell.Io
import qs.Common
import qs.Services

Singleton {
    id: root

    property int refCount: 0

    property bool sysupdateAvailable: false

    property var availableUpdates: []
    property bool isChecking: false
    property bool isUpgrading: false
    property bool hasError: false
    property string errorMessage: ""
    property string errorCode: ""
    property var backends: []
    property string distribution: ""
    property string distributionPretty: ""
    property string pkgManager: ""
    property bool distributionSupported: false
    property string shellVersion: ""
    property string shellCodename: ""
    property string semverVersion: ""
    property var latestNews: []
    property int lastNewsCheckTimestamp: 0

    property var recentLog: []
    property int intervalSeconds: 1800
    property int lastCheckUnix: 0
    property int nextCheckUnix: 0

    function getParsedShellVersion() {
        return parseVersion(semverVersion);
    }

    function parseCommonSymbols(text) {
        return text.replace(/&lt;/g, '<')
        .replace(/&gt;/g, '>')
        .replace(/&amp;/g, '&')
        .replace(/&quot;/g, '"')
        .replace(/&#39;/g, "'")
        .replace(/&nbsp;/g, ' ')
        .replace(/&#8211;/g, '–')
        .replace(/&#8230;/g, '…')
        .trim();
    }

    function latestNewsParserProducer(match) {
        const cleanTitle = parseCommonSymbols(match[1]);

        let cleanDescription = match[3].replace(/<[^>]*>/g, '');
        cleanDescription = parseCommonSymbols(cleanDescription);

        return {
            "title": cleanTitle,
            "link": match[2].trim(),
            "description": cleanDescription,
            "pubDate": match[4] ? match[4].trim() : ""
        };
    }

    readonly property var archBasedUCSettings: {
        "listUpdatesSettings": {
            "params": [],
            "correctExitCodes": [0, 2]
        },
        "parserSettings": {
            "lineRegex": /^(\S+)\s+([^\s]+)\s+->\s+([^\s]+)$/,
            "entryProducer": function (match) {
                return {
                    "name": match[1],
                    "currentVersion": match[2],
                    "newVersion": match[3],
                    "description": `${match[1]} ${match[2]} → ${match[3]}`
                };
            }
        }
    }

    readonly property var archBasedPMSettings: function(requiresSudo) {
        return {
            "listUpdatesSettings": {
                "params": ["-Qu"],
                "correctExitCodes": [0, 1]
            },
            "upgradeSettings": {
                "params": ["-Syu"],
                "requiresSudo": requiresSudo
            },
            "parserSettings": {
                "lineRegex": /^(\S+)\s+([^\s]+)\s+->\s+([^\s]+)$/,
                "entryProducer": function (match) {
                    return {
                        "name": match[1],
                        "currentVersion": match[2],
                        "newVersion": match[3],
                        "description": `${match[1]} ${match[2]} → ${match[3]}`
                    };
                }
            }
        }
    }


    readonly property var archBasedLatestNewsSettings: {
        "url": "https://archlinux.org/feeds/news/",
        "parserSettings": {
            "lineRegex": /<item>\s*<title>([^<]+)<\/title>\s*<link>([^<]+)<\/link>\s*<description>([\s\S]*?)<\/description>[\s\S]*?<pubDate>([^<]+)<\/pubDate>/g,
            "entryProducer": latestNewsParserProducer
        }
    }

    readonly property var fedoraBasedLatestNewsSettings: {
        "url": "https://fedoramagazine.org/feed/",
        "parserSettings": {
            "lineRegex": /<item>\s*<title>([^<]+)<\/title>\s*<link>([^<]+)<\/link>[\s\S]*?<pubDate>([^<]+)<\/pubDate>[\s\S]*?<description>([\s\S]*?)<\/description>[\s\S]*?/g,
            "entryProducer": function(match) {
                const cleanTitle = parseCommonSymbols(match[1]);

                let cleanDescription = match[4].replace(/<[^>]*>/g, '').replace("]]>", '');
                cleanDescription = parseCommonSymbols(cleanDescription);

                return {
                    "title": cleanTitle,
                    "link": match[2].trim(),
                    "description": cleanDescription,
                    "pubDate": match[3] ? match[3].trim() : ""
                };
            }
        }
    }

    readonly property var customLatestNewsSettings: {
        "url": SettingsData.updaterLatestNewsUrl,
        "parserSettings": {
            "lineRegex": RegExp(SettingsData.updaterLatestNewsRegex, "g"),
            "entryProducer": latestNewsParserProducer
        }
    }

    readonly property var fedoraBasedPMSettings: {
        "listUpdatesSettings": {
            "params": ["list", "--upgrades", "--quiet", "--color=never"],
            "correctExitCodes": [0, 1]
        },
        "upgradeSettings": {
            "params": ["upgrade"],
            "requiresSudo": true
        },
        "parserSettings": {
            "lineRegex": /^([^\s]+)\s+([^\s]+)\s+.*$/,
            "entryProducer": function (match) {
                return {
                    "name": match[1],
                    "currentVersion": "",
                    "newVersion": match[2],
                    "description": `${match[1]} → ${match[2]}`
                };
            }
        }
    }

    readonly property var updateCheckerParams: {
        "checkupdates": archBasedUCSettings
    }
    readonly property var packageManagerParams: {
        "yay": archBasedPMSettings(false),
        "paru": archBasedPMSettings(false),
        "pacman": archBasedPMSettings(true),
        "dnf": fedoraBasedPMSettings
    }
    readonly property var latestNewsParserParams: {
        "arch": archBasedLatestNewsSettings,
        "cachyos": archBasedLatestNewsSettings,
        "manjaro": archBasedLatestNewsSettings,
        "endeavouros": archBasedLatestNewsSettings,
        "fedora": fedoraBasedLatestNewsSettings,
        "custom": customLatestNewsSettings
    }

    readonly property list<string> supportedDistributions: ["arch", "cachyos", "manjaro", "endeavouros", "fedora"]
    readonly property int updateCount: availableUpdates.length
    readonly property bool helperAvailable: sysupdateAvailable && backends.length > 0

    Connections {
        target: DMSService
        function onCapabilitiesReceived() {
            root.checkCapabilities();
        }
        function onConnectionStateChanged() {
            if (DMSService.isConnected) {
                root.checkCapabilities();
            } else {
                root.sysupdateAvailable = false;
                root._startupCheckDone = false;
            }
            Qt.callLater(() => root._maybeStartupCheck());
        }
        function onSysupdateStateUpdate(data) {
            root._applyState(data);
        }
    }

    Connections {
        target: SettingsData
        function onUpdaterCheckOnStartChanged() {
            Qt.callLater(() => root._maybeStartupCheck());
        }
        function on_HasLoadedChanged() {
            Qt.callLater(() => root._maybeStartupCheck());
        }
    }

    Component.onCompleted: {
        if (DMSService.dmsAvailable) {
            checkCapabilities();
        }
        Qt.callLater(() => root._maybeStartupCheck());
    }

    function checkCapabilities() {
        if (!DMSService.capabilities || !Array.isArray(DMSService.capabilities)) {
            sysupdateAvailable = false;
            Qt.callLater(() => root._maybeStartupCheck());
            return;
        }
        const has = DMSService.capabilities.includes("sysupdate");
        if (has && !sysupdateAvailable) {
            sysupdateAvailable = true;
            requestState();
        } else if (!has) {
            sysupdateAvailable = false;
        }
        Qt.callLater(() => root._maybeStartupCheck());
    }

    function requestState() {
        if (!DMSService.isConnected || !sysupdateAvailable) {
            return;
        }
        DMSService.sysupdateGetState(resp => {
            if (resp && resp.result) {
                _applyState(resp.result);
            }
        });
    }

    function _applyState(data) {
        if (!data) {
            return;
        }
        availableUpdates = data.packages || [];
        backends = data.backends || [];
        distribution = data.distro || "";
        distributionPretty = data.distroPretty || "";
        distributionSupported = (backends.length > 0);
        recentLog = data.recentLog || [];
        intervalSeconds = data.intervalSeconds || 1800;
        lastCheckUnix = data.lastCheckUnix || 0;
        nextCheckUnix = data.nextCheckUnix || 0;

        const phase = data.phase || "idle";
        switch (phase) {
        case "refreshing":
            isChecking = true;
            isUpgrading = false;
            break;
        case "upgrading":
            isChecking = false;
            isUpgrading = true;
            break;
        default:
            isChecking = false;
            isUpgrading = false;
        }

        if (data.error) {
            hasError = true;
            errorMessage = data.error.message || "";
            errorCode = data.error.code || "";
        } else {
            hasError = false;
            errorMessage = "";
            errorCode = "";
        }

        if (backends.length > 0) {
            const sys = backends.find(b => b.repo === "system" || b.repo === "ostree");
            pkgManager = sys ? sys.id : backends[0].id;
        } else {
            pkgManager = "";
        }
    }

    Process {
        id: latestNewsChecker
        command: ["curl", SettingsData.updaterLatestNewsUrl || latestNewsParserParams[distribution].url]
        onExited: exitCode => {
            const correctExitCodes = [0];
            if (correctExitCodes.includes(exitCode)) {
                lastNewsCheckTimestamp = Date.now();
                parseLatestNews(stdout.text);
            }
        }

        stdout: StdioCollector {}
    }


    function parseLatestNews(feed) {
        const isCustom = !!SettingsData.updaterLatestNewsUrl && !!SettingsData.updaterLatestNewsRegex;
        const parserParams = isCustom ? latestNewsParserParams["custom"] : latestNewsParserParams[distribution];
        const regex = parserParams.parserSettings.lineRegex;
        const entryProducer = parserParams.parserSettings.entryProducer;

        const news = [];
        let match;

        // Use exec() in a loop to get all matches with global regex
        while ((match = regex.exec(feed)) !== null) {
            news.push(entryProducer(match));
        }

        latestNews = news;
    }

    function checkForUpdates() {
        DMSService.sysupdateRefresh(false, null);

        if (SettingsData.updaterShowLatestNews && Date.now() > lastNewsCheckTimestamp + 10) {
            latestNewsChecker.running = true;
        }
    }

    function runUpdates(opts) {
        const params = opts || {};
        if (SettingsData.updaterUseCustomCommand && SettingsData.updaterCustomCommand.length > 0) {
            _runCustomTerminalCommand();
            return;
        }
        DMSService.sysupdateUpgrade(params, null);
    }

    function cancelUpdates() {
        DMSService.sysupdateCancel(null);
    }

    function setInterval(seconds) {
        DMSService.sysupdateSetInterval(seconds, null);
    }

    function _runCustomTerminalCommand() {
        const terminal = SessionData.resolveTerminal();
        if (!terminal || terminal.length === 0) {
            ToastService.showError(I18n.tr("No terminal configured"), I18n.tr("Pick a terminal in Settings → Launcher (or set $TERMINAL)."));
            return;
        }
        const updateCommand = `${SettingsData.updaterCustomCommand} && echo -n "Updates complete! " ; echo "Press Enter to close..." && read`;
        const termClass = SettingsData.updaterTerminalAdditionalParams || "";
        var argv = [terminal];
        if (termClass.length > 0) {
            argv = argv.concat(termClass.split(" "));
        }
        argv.push("-e");
        argv.push("sh");
        argv.push("-c");
        argv.push(updateCommand);
        customRunner.command = argv;
        customRunner.running = true;
    }

    Process {
        id: customRunner
    }

    property bool _startupCheckDone: false

    function _maybeStartupCheck() {
        if (refCount <= 0) {
            _startupCheckDone = false;
            return;
        }
        if (!SettingsData.updaterCheckOnStart)
            return;
        if (_startupCheckDone)
            return;
        if (!DMSService.isConnected || !sysupdateAvailable)
            return;
        _startupCheckDone = true;
        Qt.callLater(() => root.checkForUpdates());
    }

    onRefCountChanged: {
        if (refCount <= 0)
            _startupCheckDone = false;
        _syncAcquire();
        Qt.callLater(() => root._maybeStartupCheck());
    }
    onSysupdateAvailableChanged: _syncAcquire()

    property bool _acquired: false

    function _syncAcquire() {
        const want = refCount > 0 && sysupdateAvailable;
        if (want === _acquired) {
            return;
        }
        _acquired = want;
        if (want) {
            DMSService.sysupdateAcquire(null);
            return;
        }
        DMSService.sysupdateRelease(null);
    }

}
